# S3, DynamoDB Table 생성

```terraform
# backend.tf

provider "aws" {  
  region = "us-west-2"  
}  
  
  
resource "aws_s3_bucket" "terraform-backend" {  
  bucket = "hanggi-terraform-backend"  
  
  lifecycle {  
    prevent_destroy = true  
  }  
}  
  
  
resource "aws_s3_bucket_versioning" "terraform-backend-versioning" {  
  bucket = aws_s3_bucket.terraform-backend.id  
  
  versioning_configuration {  
    status = "Enabled"  
  }  
}  
  
  
resource "aws_s3_bucket_server_side_encryption_configuration" "terraform-backend-encryption" {  
  bucket = aws_s3_bucket.terraform-backend.id  
  
  rule {  
    apply_server_side_encryption_by_default {  
      sse_algorithm = "AES256"  
    }  
  }  
}  
  
  
resource "aws_s3_bucket_public_access_block" "terraform-backend-public-access" {  
  bucket                  = aws_s3_bucket.terraform-backend.id  
  block_public_acls       = true  
  block_public_policy     = true  
  ignore_public_acls      = true  
  restrict_public_buckets = true  
}  
  
  
resource "aws_dynamodb_table" "terraform-state-lock" {  
  name         = "hanggi-terraform-state-lock"  
  billing_mode = "PAY_PER_REQUEST"  
  hash_key     = "LockID"  
  
  attribute {  
    name = "LockID"  
    type = "S"  
  }  
}  
  
  
output "terraform-backend-arn" {  
  value       = aws_s3_bucket.terraform-backend.arn  
  description = "The ARN of the Terraform Backend S3 bucket"  
}  
  
  
output "dynamodb-name" {  
  value       = aws_dynamodb_table.terraform-state-lock.name  
  description = "The name of the DynamoDB table"  
}
```

- 백엔드 tfstate 저장을 위한 S3, Lock state 저장을 위한 DynamoDB 테이블 생성

# 테라폼 백엔드 구성

```terraform
# backend.tf

terraform {  
  backend "s3" {  
    bucket         = "hanggi-terraform-backend"  
    key            = "terraform/terraform.tfstate"  
    region         = "us-west-2"  
    dynamodb_table = "hanggi-terraform-state-lock"  
    encrypt = true  
  }  
}

...
```

- 생성한 리소스에 테라폼 백엔드 구성하는 코드 추가

# 구성 확인


![](https://i.imgur.com/Kn6nKV9.png)


![](https://i.imgur.com/UP18Nv2.png)
